apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'


android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 3
        versionName "0.3.2"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'foundation.e:privacymodule.api:0.3.1'

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'foundation.e'
            //You can either define these here or get them from project conf elsewhere
            artifactId 'privacymodule.e-29'
            version android.defaultConfig.versionName
            artifact "$buildDir/outputs/aar/privacymodulese-release.aar" //aar artifact you want to publish
//            artifact "$buildDir/outputs/aar/privacymodulese-debug.aar" //aar artifact you want to publish

            //generate pom nodes for dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each { dependency ->
                    if (dependency.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }

            repositories {
                maven {
                    url "https://gitlab.e.foundation/api/v4/projects/781/packages/maven"
                    credentials(HttpHeaderCredentials) {
                        name = "Private-Token"
                        value = gitLabPrivateToken
                        // the variable resides in ~/.gradle/gradle.properties
                    }
                    authentication {
                        header(HttpHeaderAuthentication)
                    }
                }
            }
        }
    }
}
